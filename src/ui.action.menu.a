;license:MIT
;(c) 2023-2024 by 4am
;

; menu item indexes
kActionPlay    = 0
kActionRestore = 1
kActionBackup  = 2
kActionEdit    = 3

ActionMenuStrings
         !word sMenuPlay
         !word sMenuRestore
         !word sMenuBackup
         !word sMenuEdit
         !word sMenuBackToGame
         !byte 0

TitleStrings
         !word sTitleWizProving
         !word sTitleWizKOD
         !word sTitleWizLOL
         !word sTitleMoreOConnor
         !word sTitleMoreBrotherhood
         !word sTitleMoreVlad
         !word sTitleMoreNihonbashi
         !word sTitleMoreEmperor
         !word sTitleOtherDragonQuest
         !word sTitleOtherAtomicWasteland
         !word sTitleOtherBlackStone
         !word sTitleOtherKnightOfTheGrave

SubtitleStrings
         !word sSubtitle1981
         !word sSubtitle1982
         !word sSubtitle1983
         !word sSubtitle1985
         !word sSubtitle1985
         !word sSubtitle1987
         !word sSubtitle1987
         !word sSubtitle1987
         !word sSubtitle2024
         !word sSubtitle1987
         !word sSubtitle1987
         !word sSubtitle1987

ActionMenu
         lda   #0
         sta   ActionMenuSelectedIndex
ActionMenuRefresh
         jsr   Menu
         !word ActionMenuStrings
         !byte kScreenTopVTAB+3
ActionMenuSelectedIndex
         !byte 0
         !word ActionMenuHeader
         !word NoCallback

         bcs   @exit
         ldx   MenuSelectedIndex
         stx   ActionMenuSelectedIndex
         beq   @exitClear
         cpx   #kActionEdit
         beq   @exitClear
         cpx   #kActionBackup
         beq   ActionBackup
         cpx   #kActionRestore
         beq   ActionRestore
         sec
@exit    rts
@exitClear
         clc
         rts

ActionMenuHeader
         +LDADDR_SELECTED_GAME TitleStrings
         jsr   DrawCenteredString
         inc   VTAB
         +LDADDR_SELECTED_GAME SubtitleStrings
         jmp   DrawCenteredString

ActionBackup
         jsr   BackupScenario
         jmp   ActionMenuRefresh

ActionRestore
         jsr   RestoreMenu
         jmp   ActionMenuRefresh
