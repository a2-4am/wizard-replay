;license:MIT
;(c) 2023 by 4am
;

; menu item indexes
kActionPlay    = 0
kActionEdit    = 1
kActionBackup  = 2
kActionRestore = 3

ActionMenuStringsLo
         !byte <sMenuPlay
         !byte <sMenuEdit
         !byte <sMenuBackup
         !byte <sMenuRestore
         !byte <sMenuBack

ActionMenuStringsHi
         !byte >sMenuPlay
         !byte >sMenuEdit
         !byte >sMenuBackup
         !byte >sMenuRestore
         !byte >sMenuBack
         !byte 0

TitleStringsLo
         !byte <sTitleWiz1
         !byte <sTitleWiz2
         !byte <sTitleWiz3
         !byte <sTitleMore1
         !byte <sTitleMore2
         !byte <sTitleMore3
         !byte <sTitleMore4
         !byte <sTitleMore5

TitleStringsHi
         !byte >sTitleWiz1
         !byte >sTitleWiz2
         !byte >sTitleWiz3
         !byte >sTitleMore1
         !byte >sTitleMore2
         !byte >sTitleMore3
         !byte >sTitleMore4
         !byte >sTitleMore5

SubtitleStringsLo
         !byte <sSubtitleWiz1
         !byte <sSubtitleWiz2
         !byte <sSubtitleWiz3
         !byte <sSubtitleMore1
         !byte <sSubtitleMore2
         !byte <sSubtitleMore3
         !byte <sSubtitleMore4
         !byte <sSubtitleMore5

SubtitleStringsHi
         !byte >sSubtitleWiz1
         !byte >sSubtitleWiz2
         !byte >sSubtitleWiz3
         !byte >sSubtitleMore1
         !byte >sSubtitleMore2
         !byte >sSubtitleMore3
         !byte >sSubtitleMore4
         !byte >sSubtitleMore5

ActionMenu
         lda   #0
         sta   ActionMenuSelectedIndex
ActionMenuRefresh
         jsr   Home
         lda   #kScreenTopVTAB
         sta   VTAB
         ldx   GameSelectedIndex
         lda   TitleStringsLo, x
         ldy   TitleStringsHi, x
         jsr   DrawCenteredString
         inc   VTAB
         ldx   GameSelectedIndex
         lda   SubtitleStringsLo, x
         ldy   SubtitleStringsHi, x
         jsr   DrawCenteredString

         jsr   Menu
         !word ActionMenuStringsLo
         !word ActionMenuStringsHi
         !byte kScreenTopVTAB+3
ActionMenuSelectedIndex
         !byte 0
         !word NoCallback

         bcs   @exit
         ldx   MenuSelectedIndex
         stx   ActionMenuSelectedIndex
         beq   @exitClear
         cpx   #kActionEdit
         beq   @exitClear
         cpx   #kActionBackup
         beq   ActionBackup
         cpx   #kActionRestore
         beq   ActionRestore
         sec
@exit    rts
@exitClear
         clc
         rts

ActionBackup
         jsr   BackupScenario
         jmp   ActionMenuRefresh

ActionRestore
         jsr   Home
         lda   #kScreenTopVTAB
         sta   VTAB
         +LDADDR sRestoreWarning
         jsr   DrawCenteredString
         jsr   Menu
         !word RestoreMenuStringsLo
         !word RestoreMenuStringsHi
         !byte kScreenTopVTAB+2
         !byte 1
         !word NoCallback
         bcs   +
         ldx   MenuSelectedIndex
         bne   +
         jsr   RestoreScenario
+        jmp   ActionMenuRefresh

RestoreMenuStringsLo
         !byte <sMenuRestore
         !byte <sMenuCancel

RestoreMenuStringsHi
         !byte >sMenuRestore
         !byte >sMenuCancel
         !byte 0
