;license:MIT
;(c) 2023-2024 by 4am
;

; menu item indexes
kActionPlay    = 0
kActionRestore = 1
kActionBackup  = 2
kActionEdit    = 3

ActionMenuStrings
         !word sMenuPlay
         !word sMenuRestore
         !word sMenuBackup
         !word sMenuEdit
         !word sMenuBackToGame
         !byte 0

TitleStrings
         !word sTitleWiz1
         !word sTitleWiz2
         !word sTitleWiz3
         !word sTitleMore1
         !word sTitleMore2
         !word sTitleMore3
         !word sTitleMore4
         !word sTitleMore5
         !word sTitleOther1
         !word sTitleOther2
         !word sTitleOther3
         !word sTitleOther4

SubtitleStrings
         !word sSubtitleWiz1
         !word sSubtitleWiz2
         !word sSubtitleWiz3
         !word sSubtitleMore1
         !word sSubtitleMore2
         !word sSubtitleMore3
         !word sSubtitleMore4
         !word sSubtitleMore5
         !word sSubtitleOther1
         !word sSubtitleOther2
         !word sSubtitleOther3
         !word sSubtitleOther4

ActionMenu
         lda   #0
         sta   ActionMenuSelectedIndex
ActionMenuRefresh
         jsr   Menu
         !word ActionMenuStrings
         !byte kScreenTopVTAB+3
ActionMenuSelectedIndex
         !byte 0
         !word ActionMenuHeader
         !word NoCallback

         bcs   @exit
         ldx   MenuSelectedIndex
         stx   ActionMenuSelectedIndex
         beq   @exitClear
         cpx   #kActionEdit
         beq   @exitClear
         cpx   #kActionBackup
         beq   ActionBackup
         cpx   #kActionRestore
         beq   ActionRestore
         sec
@exit    rts
@exitClear
         clc
         rts

ActionMenuHeader
         +LDADDR_SELECTED_GAME TitleStrings
         jsr   DrawCenteredString
         inc   VTAB
         +LDADDR_SELECTED_GAME SubtitleStrings
         jmp   DrawCenteredString

ActionBackup
         jsr   BackupScenario
         jmp   ActionMenuRefresh

ActionRestore
         jsr   RestoreMenu
         jmp   ActionMenuRefresh
