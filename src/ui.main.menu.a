;license:MIT
;(c) 2023-2024 by 4am
;

MainMenuStrings
         !word sMenuWizardry
         !word sMenuWizimore
         !word sMenuOther
         ; !word sMenuUltima
         ; !word sMenuUltimore
         !word sMenuQuit
         !byte 0

MainMenuGraphicsLo
         !byte <ProvingGraphics
         !byte <OConnorGraphics
         !byte <BlackStoneGraphics
         ; !byte <BlankGraphics
         ; !byte <BlankGraphics
         !byte <BlankGraphics
MainMenuGraphicsHi0
         !byte >ProvingGraphics
         !byte >OConnorGraphics
         !byte >(KnightOfTheGraveGraphics+$400)
         ; !byte >BlankGraphics
         ; !byte >BlankGraphics
         !byte >BlankGraphics
MainMenuGraphicsHi1
         !byte >(ProvingGraphics+$200)
         !byte >(OConnorGraphics+$200)
         !byte >AtomicWastelandGraphics
         ; !byte >BlankGraphics
         ; !byte >BlankGraphics
         !byte >BlankGraphics
MainMenuGraphicsHi2
         !byte >(ProvingGraphics+$400)
         !byte >(OConnorGraphics+$400)
         !byte >BlackStoneGraphics
         ; !byte >BlankGraphics
         ; !byte >BlankGraphics
         !byte >BlankGraphics

MainMenuActions
         !word WizardryMenu
         !word WizimoreMenu
         !word OtherMenu
         ; !word UltimaMenu
         ; !word UltimoreMenu
         !word QuitMenu

MainMenu
GameSelectedIndex=*+1
         lda   #$FF                  ; $FF = no game selected
                                     ; but byte is self-modified by Bye routine
                                     ; to positive integer after game quit
         bmi   MainMenuRefresh

         ; if a game is already selected, figure out which submenu to show
         ; and go directly there instead of showing main menu
         cmp   #kWizimoreGameOffset
         bcc   @wizardry
         cmp   #kOtherGameOffset
         bcc   @wizimore
         ; cmp   #kUltimaGameOffset
         ; bcc   @other
         ; cmp   #kUltimoreGameOffset
         ; bcc   @ultima
         ;TODO rest of menus
         jmp   $FF59
@wizardry
         lda   #0
         +HIDE_NEXT_2_BYTES
@wizimore
         lda   #1
         +HIDE_NEXT_2_BYTES
@other
         lda   #2
;          +HIDE_NEXT_2_BYTES
; @ultima
;          lda   #3
;          ;TODO reset of menus
         jmp   MainMenuDispatch

MainMenuRefresh
         jsr   Menu
         !word MainMenuStrings
         !byte kScreenTopVTAB
MainMenuSelectedIndex
         !byte 0
         !word NoCallback
         !word MainMenuSelectionChangeCallback
         bcs   +
         lda   MenuSelectedIndex
MainMenuDispatch
         sta   MainMenuSelectedIndex
         asl
         tax
         lda   MainMenuActions, x
         ldy   MainMenuActions+1, x
         +ST16 @j+1
@j       jsr   SELF_MODIFIED_WORD
         bcs   MainMenuRefresh
+        rts

; UltimoreMenu
QuitMenu
SelectBack
         pla
         pla
         sec
         rts

MainMenuSelectionChangeCallback
; in:    MenuSelectedIndex populated with new index
; out:   clobbers A/X/Y/flags
         ; draw game-specific graphics along bottom of screen
         ldx   MenuSelectedIndex
         lda   MainMenuGraphicsLo, x
         ldy   MainMenuGraphicsHi0, x
         ldx   #$02
         jsr   DrawWizGraphic

         ldx   MenuSelectedIndex
         lda   MainMenuGraphicsLo, x
         ldy   MainMenuGraphicsHi1, x
         ldx   #$10
         jsr   DrawWizGraphic

         ldx   MenuSelectedIndex
         lda   MainMenuGraphicsLo, x
         ldy   MainMenuGraphicsHi2, x
         ldx   #$1E
         jmp   DrawWizGraphic
