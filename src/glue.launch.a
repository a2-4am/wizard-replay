;license:MIT
;(c) 2023-2024 by 4am
;
; glue code to launch games
;
; Public functions
; - Launch
;

;------------------------------------------------------------------------------
; Launch
; load game and jump to it
; in:    GameSelectedIndex contains index of game to launch
;          (used a index into VolNamesLo/Hi and other arrays)
;        X = kActionPlay or kActionEdit to indicate which loader to use
;------------------------------------------------------------------------------
Launch
         cpx   #kActionEdit
         php

         +LDADDR sGameDirectory
         jsr   SetPath

         +LDADDR_SELECTED_GAME VolNames
         jsr   AddToPath

         jsr   SetPrefix
         !word gPathname

         plp
         beq   @edit
@play
         +LDADDR_SELECTED_GAME PlayLoaders
         bne   @load                 ; always branches
@edit
         +LDADDR_SELECTED_GAME EditLoaders
@load
         jsr   SetPath
         jsr   LoadFile1Shot
         !word gPathname             ; filename
         !word Loader                ; load address
         !word $FFFF                 ; max size
         !word ProDOSBuffer          ; ProDOS file buffer address ($400 bytes)

         jsr   ROM_TEXT
         jsr   ROM_HOME
         jsr   ROM_NORMAL
         lda   GameSelectedIndex
         sta   LastGameSelectedIndex
         jsr   InstallBye
         jmp   Loader
