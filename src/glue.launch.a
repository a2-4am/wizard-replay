;license:MIT
;(c) 2023 by 4am
;
; glue code to launch games
;
; Public functions
; - Launch
;

VolNamesLo
         !byte <sVolProvingGrounds
         !byte <sVolKnightOfDiamonds
         !byte <sVolLegacyOfLlylgamyn
         !byte <sVolNihonbashi
         !byte <sVolOConnor
         !byte <sVolVlad
         !byte <sVolEmperor
         !byte <sVolBrotherhood

VolNamesHi
         !byte >sVolProvingGrounds
         !byte >sVolKnightOfDiamonds
         !byte >sVolLegacyOfLlylgamyn
         !byte >sVolNihonbashi
         !byte >sVolOConnor
         !byte >sVolVlad
         !byte >sVolEmperor
         !byte >sVolBrotherhood

LauncherFilenamesLo
         !byte <sLauncherFilename1
         !byte <sLauncherFilename2
         !byte <sLauncherFilename3
         !byte <sLauncherFilename1
         !byte <sLauncherFilename1
         !byte <sLauncherFilename1
         !byte <sLauncherFilename1
         !byte <sLauncherFilename1
LauncherFilenamesHi
         !byte >sLauncherFilename1
         !byte >sLauncherFilename2
         !byte >sLauncherFilename3
         !byte >sLauncherFilename1
         !byte >sLauncherFilename1
         !byte >sLauncherFilename1
         !byte >sLauncherFilename1
         !byte >sLauncherFilename1

;------------------------------------------------------------------------------
; Launch
; load game and jump to it
; in:    SelectedIndex contains index of game to launch (index into VolNamesLo/Hi)
;------------------------------------------------------------------------------
Launch
         +LDADDR sGameDirectory
         jsr   SetPath

         ldx   SelectedIndex
         lda   VolNamesLo, x
         ldy   VolNamesHi, x
         jsr   AddToPath

         jsr   SetPrefix
         !word gPathname

         ldx   SelectedIndex
         lda   LauncherFilenamesLo, x
         ldy   LauncherFilenamesHi, x
         jsr   SetPath

         jsr   LoadFile1Shot
         !word gPathname
         !word $0800
         !word $1400
         !word $1C00

         jsr   ROM_TEXT
         jsr   ROM_HOME
         jsr   ROM_NORMAL
         jsr   InstallBye
         jmp   $0800
