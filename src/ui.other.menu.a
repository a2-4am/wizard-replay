;license:MIT
;(c) 2023-2024 by 4am
;

OtherMenuStrings
         !word sMenuOther1
         !word sMenuOther2
         !word sMenuOther3
         !word sMenuOther4
         !word sMenuBackToMain
         !byte 0

OtherGraphicsLo
         !byte <BlankGraphics
         !byte <BlankGraphics
         !byte <BlankGraphics
         !byte <BlankGraphics
         !byte <BlankGraphics
OtherGraphicsHi0
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
OtherGraphicsHi1
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
OtherGraphicsHi2
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics
         !byte >BlankGraphics

OtherMenuActions
         !word SelectOther1
         !word SelectOther2
         !word SelectOther3
         !word SelectOther4
         !word SelectOtherBack

OtherMenu
         lda   GameSelectedIndex
         bmi   OtherMenuRefresh
         ; if a game is already selected, figure out which game to show
         ; and go directly there instead of showing game menu
         sec
         sbc   #kOtherGameOffset
         jmp   OtherMenuDispatch

OtherMenuRefresh
         jsr   Menu
         !word OtherMenuStrings
         !byte kScreenTopVTAB
OtherMenuSelectedIndex
         !byte 0
         !word NoCallback
         !word OtherMenuSelectionChangeCallback
         bcs   +
         lda   MenuSelectedIndex
OtherMenuDispatch
         sta   OtherMenuSelectedIndex
         asl
         tax
         lda   OtherMenuActions, x
         ldy   OtherMenuActions+1, x
         +ST16 @j+1
@j       jsr   SELF_MODIFIED_WORD
         bcs   OtherMenuRefresh
+        rts

SelectOther1
SelectOther2
SelectOther3
SelectOther4
         lda   OtherMenuSelectedIndex
         clc
         adc   #kOtherGameOffset
         sta   GameSelectedIndex
         jmp   ActionMenu

SelectOtherBack
         ldx   #$FF
         stx   GameSelectedIndex
         inx
         stx   OtherMenuSelectedIndex
         pla
         pla
         sec
         rts

OtherMenuSelectionChangeCallback
; in:    MenuSelectedIndex populated with new index
; out:   clobbers A/X/Y/flags
         ; draw game-specific graphics along bottom of screen
         ldx   MenuSelectedIndex
         lda   OtherGraphicsLo, x
         ldy   OtherGraphicsHi0, x
         ldx   #$02
         jsr   DrawWizGraphic

         ldx   MenuSelectedIndex
         lda   OtherGraphicsLo, x
         ldy   OtherGraphicsHi1, x
         ldx   #$10
         jsr   DrawWizGraphic

         ldx   MenuSelectedIndex
         lda   OtherGraphicsLo, x
         ldy   OtherGraphicsHi2, x
         ldx   #$1E
         jmp   DrawWizGraphic
