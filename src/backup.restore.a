;license:MIT
;(c) 2023-2024 by 4am
;

BackupScenario
; in:    GameSelectedIndex populated
; out:   C=0 if success
;        C=1 if failure, and A=MLI error code
         jsr   Home
         inc   VTAB
         inc   VTAB
         +LDADDR sBackup
         jsr   DrawCenteredString
         inc   VTAB
         inc   VTAB
         +LDADDR sInitialProgressBar
         ldx   #$02
         jsr   DrawString
         lda   #$03
         sta   HTAB

         jsr   BuildPathToScenario
         jsr   OpenSourceFile
         bcs   ExitBackupRestore
         sta   BackupRestoreSourceRefNum

         jsr   BuildPathToScenarioBackup
         jsr   OpenDestFile
         bcs   ExitBackupRestore
         sta   BackupRestoreDestRefNum
         bcc   BackupRestoreLoop     ; always branches

RestoreScenario
         jsr   Home
         inc   VTAB
         inc   VTAB
         +LDADDR sRestore
         jsr   DrawCenteredString
         inc   VTAB
         inc   VTAB
         +LDADDR sInitialProgressBar
         ldx   #$02
         jsr   DrawString
         lda   #$03
         sta   HTAB

         jsr   BuildPathToScenarioBackup
         jsr   OpenSourceFile
         bcs   ExitBackupRestore
         sta   BackupRestoreSourceRefNum

         jsr   BuildPathToScenario
         jsr   OpenDestFile
         bcs   ExitBackupRestore
         sta   BackupRestoreDestRefNum
         ; /!\ execution falls through here

BackupRestoreLoop
         jsr   ReadFile
BackupRestoreSourceRefNum
         !byte SELF_MODIFIED_BYTE
         !word $0800
         !word $1000
         bcc   +
         cmp   #$4C                  ; EOF = done
         beq   ExitBackupRestoreWithNoError
         bne   ExitBackupRestoreWithError

+
         +LDADDR sProgressBlock
         ldx   HTAB
         jsr   DrawString
         inc   HTAB

         jsr   WriteFile
BackupRestoreDestRefNum
         !byte SELF_MODIFIED_BYTE
         !word $0800
         !word $1000
         bcc   BackupRestoreLoop

ExitBackupRestoreWithError
         sec
         +HIDE_NEXT_BYTE
ExitBackupRestoreWithNoError
         clc
ExitBackupRestore
         php                         ; save status
         pha                         ; save error code (if C=1)
         jsr   CloseAllFiles
         pla                         ; restore error code
         plp                         ; restore status
         rts

OpenSourceFile
         jsr   OpenFile
         !word gPathname
         !word $1800
         bcs   +
         jsr   SetMarkTo0
+        rts

OpenDestFile
         jsr   OpenFile
         !word gPathname
         !word $1C00
         bcs   +
         jsr   SetMarkTo0
+        rts

BuildPathToScenario
; in:    GameSelectedIndex populated
         +LDADDR sGameDirectory
         jsr   SetPath

         +LDADDR_SELECTED_GAME VolNames
         jsr   AddToPath

         +LDADDR sSlash
         jsr   AddToPath

         +LDADDR_SELECTED_GAME Scenarios
         jmp   AddToPath

BuildPathToScenarioBackup
; in:    GameSelectedIndex populated
         +LDADDR sGameDirectory
         jsr   SetPath

         +LDADDR_SELECTED_GAME VolNames
         jsr   AddToPath

         +LDADDR sSlash
         jsr   AddToPath

         +LDADDR_SELECTED_GAME Backups
         jmp   AddToPath
